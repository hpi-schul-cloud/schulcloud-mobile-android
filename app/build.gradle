apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'
apply plugin: "org.jlleitschuh.gradle.ktlint"

kotlin {
    experimental {
        coroutines "enable"
    }
}

android {
    buildToolsVersion rootProject.buildToolsVersion
    compileSdkVersion rootProject.compileSdkVersion

    dataBinding.enabled = true

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion

        versionName rootProject.versionName
        versionCode rootProject.versionCode

        applicationId "org.schulcloud.mobile"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    productFlavors.all {
        ext.appName = "Schul-Cloud"
        ext.appBackend = null
    }

    flavorDimensions "brand"

    productFlavors {
        hpi {
            buildConfigField "String", "API_URL", '"https://schul-cloud.org:8080"'
        }

        n21 {
            applicationId "cloud.niedersachsen.mobile"
            buildConfigField "String", "API_URL", '"https://niedersachsen.cloud:8080"'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled false
            debuggable true

            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
        }
    }

    lintOptions {
        abortOnError false
    }
}

ktlint {
    version = "0.21.0"
    android = true
    reporters = org.jlleitschuh.gradle.ktlint.reporter.ReporterType.CHECKSTYLE
    ignoreFailures = true
}

detekt {
    version = "1.0.0.RC8"
    profile("main") {
        input = "$projectDir"
        config = "$rootDir/detekt.yml"
        filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
        output = "$projectDir/build/reports/detekt"
    }
}

apply from: 'dependencies.gradle'
